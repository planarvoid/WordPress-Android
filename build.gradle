def buildSrcDir = 'buildsystem'
apply from: "${buildSrcDir}/setup.gradle"
apply from: "${buildSrcDir}/tests.gradle"

buildscript {
  apply from: "buildsystem/dependencies.gradle"
  def gradlePlugins = rootProject.ext.gradlePlugins

  repositories {
    mavenLocal()
    maven { url 'http://maven.int.s-cloud.net/content/groups/soundcloud-proxy' }
  }
  dependencies {
    classpath gradlePlugins.android
    classpath gradlePlugins.gms
  }
}

allprojects {
  ext {
    manifestSrcFile = 'AndroidManifest.xml'
    javaSrcDirs = 'src/main/java'
    resSrcDirs = 'src/main/res'
    assetsSrcDirs = 'assets'

    androidNetworkManagerApplicationId = 'com.soundcloud.androidnetworkmanager'

    mobileTestRunnerUser = "mobile"
    mobileTestRunnerHost = envOr('MTR_HOST', 'chaos-slave.mobile.s-cloud.net')
    mobileTestRunnerSshPort = envOr('MTR_SSH_PORT', '22')
    mobileTestRunner = "com.soundcloud.mobiletestrunner:client:138-36-854afc4"
    mobileTestRunnerUrl = envOr('MTR_URL', "http://$mobileTestRunnerHost:5000")
    mobileTestRunnerReportUrl = "http://mobile-jenkins.int.s-cloud.net:84"
    mobileTestRunnerMaxTestAttempts = 3
    mobileTestRunnerScheduleAllTestAttemptsAtOnce = false
    mobileTestRunnerMaxCollectionTaskAttempts = 3
  }
  repositories {
    mavenLocal()
    maven { url 'http://maven.int.s-cloud.net/content/groups/soundcloud-proxy' }
  }

  // Allow 1000 errors instead of default, as errors from failure to generate code can bury real errors
  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xmaxerrs" << "1000"
    }
  }

}

task deployDevDebug(type: Exec, dependsOn: 'app:installDevDebug') {
  def rootDir = project.rootDir
  def localProperties = new File(rootDir, "local.properties")
  if (localProperties.exists()) {
    Properties properties = new Properties()
    localProperties.withInputStream {
      inputStream -> properties.load(inputStream)
    }
    def sdkDir = properties.getProperty('sdk.dir')
    def adb = "$sdkDir/platform-tools/adb"
    commandLine "$adb", 'shell', 'am', 'start', '-n', 'com.soundcloud.android.debug/com.soundcloud.android.main.LauncherActivity'
  }
}

def pullDb(def variant) {
  exec { commandLine 'adb', 'shell', "run-as com.soundcloud.android.$variant chmod 666 /data/data/com.soundcloud.android.$variant/databases/SoundCloud" }
  exec { commandLine 'adb', 'shell', "run-as com.soundcloud.android.$variant cat /data/data/com.soundcloud.android.$variant/databases/SoundCloud > /sdcard/sc-${variant}.db" }
  exec { commandLine 'adb', 'pull', "/sdcard/sc-${variant}.db" }
  exec { commandLine 'adb', 'shell', "rm -f /sdcard/sc-${variant}.db" }
}

task pullDebugDb() {
  doLast {
    pullDb("debug")
  }
}

task pullAlphaDb() {
  doLast {
    pullDb("alpha")
  }
}

def envOr(String key, String defaultValue) {
  System.getenv(key)?:defaultValue
}

