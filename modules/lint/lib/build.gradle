apply plugin: 'com.android.library'

android {
  compileSdkVersion rootProject.ext.androidCompileSdkVersion
  buildToolsVersion rootProject.ext.androidBuildToolsVersion

  defaultConfig {
    targetSdkVersion rootProject.ext.androidTargetSdkVersion
    minSdkVersion rootProject.ext.androidMinSdkVersion
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }
}

configurations {
  lintChecks
}

dependencies {
  lintChecks project(path: ":modules:lint:rules", configuration: "lintChecks")
}

task copyLintJar(type: Copy){
  from (configurations.lintChecks) {
    rename {
      String fileName ->
        'lint.jar'
    }
  }
  into 'build/intermediates/lint/'
}

project.afterEvaluate{
  def compileLintTask = project.tasks.find {it.name == 'compileLint'}
  compileLintTask.dependsOn(copyLintJar)
}
