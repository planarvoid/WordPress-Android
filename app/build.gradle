import java.util.regex.Pattern

int getVersionCode() {
  def manifestFile = file(rootProject.ext.manifestSrcFile)
  def manifestText = manifestFile.getText()
  def patternVersionCode = Pattern.compile('versionCode=\"(\\d+)\"')
  def matcherVersionCode = patternVersionCode.matcher(manifestText)
  matcherVersionCode.find()

  return Integer.parseInt(matcherVersionCode.group(1))
}

ext {
  debugVersionCode        = getVersionCode()
  alphaVersionCode        = 1000
  betaVersionCode         = debugVersionCode + 1
  releaseVersionCode      = debugVersionCode + 2
  followUpBetaVersionCode = debugVersionCode + 3
  versionName             = new Date().format('yyyy.MM.dd')
  releaseBody             = System.env.containsKey('RELEASE_BODY') ? System.env.RELEASE_BODY : '' //'' signals local build
  releaseName             = System.env.containsKey('RELEASE_NAME') ? System.env.RELEASE_NAME : '' //'' signals local build
  pipelineVersion         = System.env.containsKey('PIPELINE_VERSION') ? System.env.PIPELINE_VERSION : '0-0' // '0-0' signals local build
  buildType               = System.env.containsKey('BUILD_TYPE') ? System.env.BUILD_TYPE : 'debug' // 'debug' signals local build
  buildFlavor             = System.env.containsKey('JENKINS_URL') ? 'prod' : 'dev' // 'prod' signals CI build; 'dev' signals local build
  gitBranch               = System.env.containsKey('GIT_BRANCH') ? System.env.GIT_BRANCH.replace('origin/','') : '' // '' signals local build

  rootPackage               = "com.soundcloud.android"
  applicationId             = provideApplicationId()
  testApplicationId         = "${applicationId}.tests"
  testInstrumentationRunner = "${rootPackage}.framework.runner.RandomizingRunner"

  outputsDir            = "$project.buildDir/outputs/apk"
  apkPath               = "$outputsDir/soundcloud-android-$versionName-${pipelineVersion}.apk" // only used by renameApk task (for CI)
  testApkPath           = "$outputsDir/app-$buildFlavor-$buildType-androidTest.apk"
  networkManagerApkPath = "$outputsDir/androidnetworkmanager-${rootProject.('networkManager')}.apk"

  buildSrcDir = 'buildsystem'
}

private def provideApplicationId() {
  boolean isReleaseBuild = (buildType == 'preRelease' || buildType == 'beta' || buildType == 'release')
  return isReleaseBuild ? rootPackage : "${rootPackage}.${buildType}"
}

buildscript {
  repositories {
    mavenLocal()
    maven { url 'http://maven.int.s-cloud.net/content/groups/soundcloud-proxy' }
  }
  dependencies {
    classpath 'io.fabric.tools:gradle:1.22.0'
    classpath 'com.fernandocejas.frodo:frodo-plugin:0.8.2'
    classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.1'
    classpath "gradle.plugin.com.nimbledroid:gradle-profiler:1.0.8"
    classpath 'me.tatarka:gradle-retrolambda:3.3.1'
  }
}

apply from: "${buildSrcDir}/build_variants.gradle"
apply from: "${buildSrcDir}/feature_flags.gradle"
apply from: "${buildSrcDir}/app_properties.gradle"
apply from: "${buildSrcDir}/versionizer.gradle"
apply from: "${buildSrcDir}/slackbot.gradle"
apply from: "${buildSrcDir}/static_analysis.gradle"
apply from: "${buildSrcDir}/apublisher.gradle"
apply from: "${buildSrcDir}/git.gradle"
apply from: "${buildSrcDir}/github.gradle"
apply from: "${buildSrcDir}/ci.gradle"
apply from: "${buildSrcDir}/networkmanager.gradle"
apply from: "${buildSrcDir}/mobile_testrunner.gradle"
apply from: "${buildSrcDir}/system_utilities.gradle"

apply plugin: 'com.android.application'
//You can enable it when need it.
//apply plugin: 'com.fernandocejas.frodo'
apply plugin: 'maven'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: "com.nimbledroid.profiler"
apply plugin: 'me.tatarka.retrolambda'

android {
  compileSdkVersion rootProject.ext.androidCompileSdkVersion
  buildToolsVersion rootProject.ext.androidBuildToolsVersion

  defaultConfig {
    targetSdkVersion rootProject.ext.androidTargetSdkVersion
    applicationId project.ext.rootPackage
    testApplicationId project.ext.testApplicationId
    testInstrumentationRunner project.ext.testInstrumentationRunner
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  sourceSets {
    main {
      manifest.srcFile rootProject.ext.manifestSrcFile
      java.srcDirs rootProject.ext.javaSrcDirs
      res.srcDirs rootProject.ext.resSrcDirs
      assets.srcDirs rootProject.ext.assetsSrcDirs
      aidl.srcDirs rootProject.ext.javaSrcDirs
    }
  }

  lintOptions {
    abortOnError false
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }

  dexOptions {
    jumboMode true
    javaMaxHeapSize "4g"
  }
}

nimbledroid {
  def propertiesFile = file("$System.env.HOME/.gradle/nimbledroid.properties")
  if (propertiesFile.exists() && propertiesFile.canRead()) {
    def nimbledroidProperties = new Properties()
    nimbledroidProperties.load(new FileInputStream(propertiesFile))

    apkFilename "app-prod-release.apk"
    apiKey nimbledroidProperties.getProperty('apiKey')
    appData {
      username nimbledroidProperties.getProperty('username')
      password nimbledroidProperties.getProperty('password')
    }
  }
}

configurations {
  testSupport // for exporting our test helpers
}

task testSupportJar(type: Jar, dependsOn: ':app:compileDevDebugUnitTestSources') {
  from "$project.buildDir/intermediates/classes/test/dev/debug"
  exclude "**/*Test.class"
  exclude "**/*Test\$*.class"
  includeEmptyDirs = false
  archiveName "test-support.jar"
}

artifacts {
  testSupport testSupportJar
}

dependencies {
  def libraries = rootProject.ext.libraries
  def acceptanceTestLibraries = rootProject.ext.acceptanceTestLibraries

  /* Compile/Dev Time */
  annotationProcessor libraries.daggerCompiler
  annotationProcessor libraries.lightCycleProcessor
  annotationProcessor libraries.autoValue
  annotationProcessor libraries.autoFactory
  annotationProcessor libraries.autoParcelProcessor
  annotationProcessor libraries.butterKnifeCompiler

  provided libraries.findBugsAnnotations
  provided libraries.findBugsJsr305
  provided libraries.intellijAnnotations
  provided libraries.javaxAnnotations // for auto-value

  /* SoundCloud Libraries */
  compile libraries.androidKit
  compile libraries.propeller
  compile libraries.skippy
  compile libraries.flipper
  compile libraries.soundCloudVorbisEncoder
  compile libraries.lightCycle

  /* 3rd Party Libraries */
  compile libraries.autoParcel
  compile libraries.comscore
  compile libraries.adjustAndroid
  compile libraries.dagger
  compile (libraries.crashlytics) {
    transitive = true
  }
  compile libraries.jacksonRespack
  compile libraries.okHttp
  compile (libraries.slidingUpPanel) {
    transitive = true;
  }
  compile libraries.tSnackBar
  compile libraries.androidCrop
  compile libraries.rxAndroid
  compile libraries.rxJava
  compile libraries.rxJavaAsyncUtil // remove when getting rid of LegacyCommand
  compile libraries.universalImageLoader
  compile libraries.facebookRebound
  compile(libraries.facebookAndroidSdk) {
      // see https://github.com/mente/facebook-api-android-aar/issues/11
      transitive = true
  }
  compile libraries.butterKnife
  compile libraries.castCompanionLibrary
  compile libraries.undoBar
  compile libraries.appBoy
  compile libraries.krux
  compile libraries.moat
  compile (libraries.tapTargetView) {
    exclude group: 'com.android.support'
  }

  /* Android ... keep order! */
  compile libraries.androidMultidex
  compile libraries.androidDesign
  compile libraries.androidSupportLibrary
  compile libraries.androidAppCompatLibrary
  compile libraries.androidMediaRouterLibrary
  compile libraries.androidRecyclerView
  compile libraries.androidCardView
  compile libraries.androidPlayServicesBase
  compile libraries.androidPlayServicesAuth
  compile libraries.androidPlayServicesCastLegacy
  compile libraries.androidPlayServicesCast
  compile libraries.firebaseCore
  compile libraries.firebaseConfig
  compile libraries.firebaseMessaging
  compile libraries.firebaseAnalytics

  /* Unit tests */
  testCompile unitTestLibraries.junit
  testCompile unitTestLibraries.assertj
  testCompile unitTestLibraries.assertjAndroid
  testCompile unitTestLibraries.androidSupportAnnotations
  testCompile unitTestLibraries.mockito
  testCompile unitTestLibraries.robolectric
  testCompile unitTestLibraries.robolectricSupport
  testCompile unitTestLibraries.modelCitizen
  testCompile unitTestLibraries.equalsVerifier
  testCompile unitTestLibraries.sqliteJdbc

  /* Acceptance Tests */
  androidTestCompile(libraries.androidMultidexInstrumentation) {
    exclude group: 'com.android.support', module: 'multidex'
  }
  androidTestCompile acceptanceTestLibraries.hamcrest
  androidTestCompile acceptanceTestLibraries.robotium
  androidTestCompile acceptanceTestLibraries.networkManagerClient
}

//ADD THIS AT THE BOTTOM
//https://firebase.google.com/docs/android/setup
apply plugin: 'com.google.gms.google-services'
