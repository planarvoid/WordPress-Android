import com.soundcloud.playpublisher.*

buildscript {
  repositories {
    maven { url 'http://maven.int.s-cloud.net/content/groups/soundcloud-proxy' }
  }
  dependencies {
    classpath 'com.soundcloud:play-publisher:1.0.0'
  }
}

apply from: "${project.('ext').buildSrcDir}/apk_finder.gradle"

def productionAppConfiguration =  new PublishConfiguration(file("$rootProject.projectDir/production_client_secrets.json"), project(':app').applicationId)

task uploadBeta(type: BetaPublishTask) {
  description = 'Uploads app to Google Play Store Beta track.'
  publishConfiguration = productionAppConfiguration
  apkFileProvider = { findApkPath("beta", project.('betaVersionCode')) }
}

task uploadFollowUpBeta(type: BetaPublishTask) {
  description = 'Uploads app to Google Play Store Beta track following Production rollout.'
  publishConfiguration = productionAppConfiguration
  apkFileProvider = { findApkPath("beta", project.('followUpBetaVersionCode')) }
}

task rolloutOnePercent(type: RolloutPublishTask) {
  description = 'Uploads app to Google Play Store Staged Rollout track and rolls the app out to 1% of user base.'
  publishConfiguration = productionAppConfiguration
  apkFileProvider = { findApkPath("release", project.('releaseVersionCode')) }
  userFraction = UserFraction.ONE_PERCENT
}

task rolloutTwentyPercent(type: UpdateRolloutTask) {
  description = 'Rolls the app out to 20% of user base.'
  publishConfiguration = productionAppConfiguration
  versionCode = project.('releaseVersionCode')
  userFraction = UserFraction.TWENTY_PERCENT
}

task rolloutFull(type: ProductionRolloutTask) {
  description = 'Rolls the app out to 100% of user base.'
  publishConfiguration = productionAppConfiguration
  versionCode = project.('releaseVersionCode')
}
